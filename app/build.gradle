plugins {
    id 'com.gladed.androidgitversion' version '0.4.10'
    id 'com.github.triplet.play' version '2.7.5'
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")
// Create this file with some default values because otherwise the Gradle build fails.
if (!keystorePropertiesFile.exists()) {
    keystorePropertiesFile.text = """\
// These are just some default values. 
storePassword=yacguide
keyPassword=yacguide_key
keyAlias=key0
storeFile=keystore.jks
playServicesFile=google-services.json
"""
}
// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

androidGitVersion {
    baseCode 1
    prefix 'v'
    tagPattern(/^$prefix[0-9]+.*/)
    hideBranches = ['master']
}

play {
    serviceAccountCredentials = rootProject.file(keystoreProperties['playServicesFile'])
    // This disables GPP by default.
    enabled = false
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.yacgroup.yacguide"
        minSdkVersion 22
        targetSdkVersion 28
        versionCode androidGitVersion.code()
        versionName androidGitVersion.name()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        archivesBaseName = "yacguide"
    }
    signingConfigs {
        gplay {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile rootProject.file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.gplay
        }
        debug {
            // This ensures that the app can be installed on the phone in parallel to any
            // already installed version.
            applicationIdSuffix ".debug"
            debuggable true
            signingConfig signingConfigs.debug
        }
    }
    flavorDimensions 'default'
    productFlavors {
        stable {
            dimension 'default'
        }
        dev {
            dimension 'default'
            applicationIdSuffix '.dev'
            versionCode 20200527
            versionName '20200527'
        }
    }
    playConfigs {
        dev {
            track = "beta"
            enabled = true
        }
    }
}

dependencies {
    // Room
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'

    def room_version = "2.2.5"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    // use kapt for Kotlin

    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"

    // optional - RxJava support for Room
    implementation "androidx.room:room-rxjava2:$room_version"

    // optional - Guava support for Room, including Optional and ListenableFuture
    implementation "androidx.room:room-guava:$room_version"

    // Test helpers
    testImplementation "androidx.room:room-testing:$room_version"

    implementation 'net.sf.kxml:kxml2:2.3.0'
    implementation 'com.caverock:androidsvg:1.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "io.noties.markwon:core:4.3.1"
}
repositories {
    mavenCentral()
}